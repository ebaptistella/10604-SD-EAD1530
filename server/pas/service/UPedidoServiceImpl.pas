unit UPedidoServiceImpl;

interface

uses
  UPedidoServiceIntf, UPizzaTamanhoEnum, UPizzaSaborEnum,
  UPedidoRepositoryIntf, UPedidoRetornoDTOImpl, UClienteServiceIntf;

type
  TPedidoService = class(TInterfacedObject, IPedidoService)
  private
    FPedidoRepository: IPedidoRepository;
    FClienteService: IClienteService;

    function calcularValorPedido(const APizzaTamanho: TPizzaTamanhoEnum): Currency;
    function calcularTempoPreparo(const APizzaTamanho: TPizzaTamanhoEnum; const APizzaSabor: TPizzaSaborEnum): Integer;
    function buscaSaborPizza(SaborBD: Integer): TPizzaSaborEnum;
    function buscaTamanhoPizza(TamanhoBD: Integer): TPizzaTamanhoEnum;
  public
    function efetuarPedido(const APizzaTamanho: TPizzaTamanhoEnum; const APizzaSabor: TPizzaSaborEnum; const ADocumentoCliente: String): TPedidoRetornoDTO;
    function consultarPedido(const ADocumentoCliente: string): TPedidoRetornoDTO; stdcall;
    constructor Create; reintroduce;

  end;

implementation

uses
  UPedidoRepositoryImpl, System.SysUtils, UClienteServiceImpl,
  FireDAC.Comp.Client;

{ TPedidoService }

function TPedidoService.buscaSaborPizza(SaborBD: Integer): TPizzaSaborEnum;
begin
  case SaborBD of
    0: Result := enCalabresa;
    1: Result := enMarguerita;
    2: Result := enPortuguesa;
  end;
end;

function TPedidoService.buscaTamanhoPizza(TamanhoBD: Integer): TPizzaTamanhoEnum;
begin
  case TamanhoBD of
    0: Result := enPequena;
    1: Result := enMedia;
    2: Result := enGrande;
  end;
end;

function TPedidoService.calcularTempoPreparo(const APizzaTamanho: TPizzaTamanhoEnum; const APizzaSabor: TPizzaSaborEnum): Integer;
begin
  Result := 15;
  case APizzaTamanho of
    enPequena:
      Result := 15;
    enMedia:
      Result := 20;
    enGrande:
      Result := 25;
  end;

  if (APizzaSabor = enPortuguesa) then
    Result := Result + 5;
end;

function TPedidoService.calcularValorPedido(const APizzaTamanho: TPizzaTamanhoEnum): Currency;
begin
  Result := 20;
  case APizzaTamanho of
    enPequena:
      Result := 20;
    enMedia:
      Result := 30;
    enGrande:
      Result := 40;
  end;
end;

function TPedidoService.consultarPedido(const ADocumentoCliente: string): TPedidoRetornoDTO;
var
  oFDQuery: TFDQuery;
begin
  oFDQuery := TFDQuery.Create(nil);
  try
    FPedidoRepository.consultarPedido(ADocumentoCliente, oFDQuery);
    if (oFDQuery.IsEmpty) then
      raise Exception.Create('O Cliente com o nº de Documento ' + ADocumentoCliente + ' não possui pedidos!!!');

    with oFDQuery do
      Result := TPedidoRetornoDTO.Create(buscaTamanhoPizza(FieldByName('en_tamanhopizza').AsInteger), buscaSaborPizza(FieldByName('en_saborpizza').AsInteger), FieldByName('vl_pedido').AsCurrency, FieldByName('nr_tempopedido').AsInteger);
  finally
    oFDQuery.Free;
  end;
end;

constructor TPedidoService.Create;
begin
  inherited;

  FPedidoRepository := TPedidoRepository.Create;
  FClienteService := TClienteService.Create;
end;

function TPedidoService.efetuarPedido(const APizzaTamanho: TPizzaTamanhoEnum; const APizzaSabor: TPizzaSaborEnum; const ADocumentoCliente: String)
  : TPedidoRetornoDTO;
var
  oValorPedido: Currency;
  oTempoPreparo: Integer;
  oCodigoCliente: Integer;
begin
  oValorPedido := calcularValorPedido(APizzaTamanho);
  oTempoPreparo := calcularTempoPreparo(APizzaTamanho, APizzaSabor);
  oCodigoCliente := FClienteService.adquirirCodigoCliente(ADocumentoCliente);

  FPedidoRepository.efetuarPedido(APizzaTamanho, APizzaSabor, oValorPedido, oTempoPreparo, oCodigoCliente);
  Result := TPedidoRetornoDTO.Create(APizzaTamanho, APizzaSabor, oValorPedido, oTempoPreparo);
end;

end.
